import net.masterthought.cucumber.ReportBuilder

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
def cucumberVersion = "1.2.4"

defaultTasks 'test'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.codehaus.gpars:gpars:1.2.1",
                "net.masterthought:cucumber-reporting:4.3.0"
    }
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cleanReports() {
    File reportOutputDirectory = new File("build/reports/cucumber")
    reportOutputDirectory.deleteDir()
}

task cucumber() {
    dependsOn clean, cleanReports, assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'net.continuumsecurity.steps', 'src/test/resources/features']
        }
        generateReport()
    }
}

task generateReportTask() {
    doLast {
        generateReport()
    }
}

task buildIt() {
    dependsOn assemble, compileTestJava, cleanReports
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    systemProperties = System.properties
    dependsOn assemble, compileTestJava, cleanReports
    finalizedBy(generateReportTask)
}

//Ensure the cucumber tests are executed as part of the build. Makes for a very pretty output.
build.dependsOn cucumber

def generateReport() {
    File reportOutputDirectory = new File("build/reports/cucumber/pretty")
    reportOutputDirectory.deleteDir()

    def jsonReports = fileTree(dir: "${reporting.baseDir}/cucumber").include '**/*.json'.toString()
    List<String> jsonReportFiles = new ArrayList<String>()
    jsonReports.each { File file ->
        jsonReportFiles.add("${reporting.baseDir}/cucumber/${file.name}".toString());
    }

    String projectName = "BDD-Security";
    boolean skippedFails = true;
    boolean pendingFails = false;
    boolean undefinedFails = true;
    boolean missingFails = true;
    boolean runWithJenkins = true;
    boolean parallelTesting = true;

    net.masterthought.cucumber.Configuration configuration = new net.masterthought.cucumber.Configuration(reportOutputDirectory, projectName);
// optionally only if you need
    configuration.setStatusFlags(skippedFails, pendingFails, undefinedFails, missingFails);
    configuration.setParallelTesting(parallelTesting);
    configuration.setRunWithJenkins(runWithJenkins);
    configuration.setBuildNumber("SecTests.1");
    ReportBuilder reportBuilder = new ReportBuilder(jsonReportFiles, configuration);
    reportBuilder.generateReports();
    println("\nReport available on: "+reportOutputDirectory.getCanonicalPath()+"/feature-overview.html")
}

dependencies {
    compile group: 'net.masterthought', name: 'cucumber-reporting', version: "4.3.0"
    compile 'junit:junit:4.12'
    compile "info.cukes:cucumber-junit:$cucumberVersion"
    compile "info.cukes:cucumber-java:$cucumberVersion"
    compile "info.cukes:cucumber-picocontainer:$cucumberVersion"

    compile 'org.glassfish.jersey.core:jersey-client:2.27'
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.9.8'
    compile 'org.glassfish.jersey.media:jersey-media-moxy:2.27'
    compile 'junit:junit-dep:4.11'
    compile 'org.hamcrest:hamcrest-all:1.3'
    compile ('org.seleniumhq.selenium:selenium-java:3.141.59') { exclude group: 'junit' }
    compile 'org.seleniumhq.selenium:selenium-api:3.141.59'
    compile 'org.seleniumhq.selenium:htmlunit-driver:2.33.3'
    compile 'log4j:log4j:1.2.17'
    compile 'args4j:args4j:2.33'
    compile 'commons-configuration:commons-configuration:1.10'
    compile 'uk.com.robust-it:cloning:1.9.11'
    implementation 'org.jline:jline:3.9.0'
    compile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'commons-jxpath:commons-jxpath:1.3'
    compile 'org.glassfish.jersey.core:jersey-client:2.27'
    compile 'org.glassfish.jersey.connectors:jersey-apache-connector:2.27'
    compile 'net.htmlparser.jericho:jericho-html:3.4'
    compile 'edu.umass.cs.benchlab:harlib:1.1.2'
    compile group: 'org.zaproxy', name: 'zap-clientapi', version: '1.6.0'
    compile files ("lib/zap-java-api-2.6.0.jar", "lib/nessus-java-client-0.2-SNAPSHOT.jar","lib/jssylze-0.2-SNAPSHOT.jar")
}